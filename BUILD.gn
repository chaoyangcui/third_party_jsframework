# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ace/ace_engine/ace_config.gni")

prebuilt_js_path =
    get_label_info(":gen_jsf_c", "root_out_dir") + "/dist/strip.native.min.js"

snapshot_path =
    get_label_info(":gen_snapshot", "target_out_dir") + "/strip.native.min.js"

action("gen_snapshot") {
  script = "//third_party/jsframework/js_framework_build.sh"

  js_framework = "//third_party/jsframework/runtime"
  node_modules = "//prebuilts/build-tools/common/js-framework/node_modules"

  buildfile = "//third_party/jsframework/build.js"

  package_file = "//third_party/jsframework/package.json"
  tsconfig = "//third_party/jsframework/tsconfig.json"
  eslint = "//third_party/jsframework/.eslintrc"
  test_file = "//third_party/jsframework/test"

  nodejs_path =
      "//prebuilts/build-tools/common/nodejs/node-v12.18.4-linux-x64/bin/node"

  args = [
    rebase_path(buildfile, root_build_dir),
    rebase_path(nodejs_path, root_build_dir),
    rebase_path(js_framework, root_build_dir),
    rebase_path(node_modules, root_build_dir),
    rebase_path(package_file, root_build_dir),
    rebase_path(tsconfig, root_build_dir),
    rebase_path(eslint, root_build_dir),
    rebase_path(test_file, root_build_dir),
  ]

  outputs = [ snapshot_path ]
}

jsfc_path = get_label_info(":jsf", "target_out_dir") + "/src/jsf.c"

action("gen_jsf_c") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.

  deps = [ ":gen_snapshot" ]
  deps += [ "//third_party/quickjs:qjsc(${host_toolchain})" ]

  script = get_label_info("//third_party/quickjs:qjsc(${host_toolchain})",
                          "root_out_dir") + "/ace/ace_engine_full/qjsc"

  args = [
    "-o",
    rebase_path(jsfc_path),
    "-N",
    "js_framework",
    "-c",
    rebase_path(prebuilt_js_path),
  ]

  inputs = [ snapshot_path ]
  outputs = [ jsfc_path ]
}

ohos_static_library("jsf") {
  deps = [ ":gen_jsf_c" ]
  sources = [ jsfc_path ]
}
